# Based on: https://hannes.enjoys.it/blog/2019/09/dynamic-elevation-profile-lines-as-qgis-geometry-generator/
# Author: HannesPost

with_variable(
  'raster_layer',
  'Peru_d87ce719_d1f9_4816_ad9a_400ad943da82',  -- RASTER LAYER to sample from
  collect_geometries(
	array_foreach(
	  generate_series(
		y(@map_extent_center)-(@map_extent_height/2), -- bottom y
		y(@map_extent_center)+(@map_extent_height/2),  -- top y
		@map_extent_height/400  -- stepsize -> HOW MANY LINES
		 ),
		 with_variable(
			 'y',
			 @element,
			 make_line(
				 array_foreach(
					generate_series(
						x(@map_extent_center)-(@map_extent_width/2), -- left x
						x(@map_extent_center)+(@map_extent_width/2),  -- right x
						@map_extent_width/450  -- stepsize -> HOW MANY POINTS PER LINE
						),
					make_point(
						@element,  -- the current value from the loop over the x value range
					    @y  -- the y value from the outer loop
						+   -- will get an additional offset to generate the effect
						coalesce(  -- coalesce to catch raster null values
							raster_value(
								@raster_layer,
								1,  -- band 1, *snore*
								transform(
									make_point(@element, @y),
									@map_crs,
									layer_property(@raster_layer,'crs')
								         )
							            ),
								0  -- coalesce 0 if raster_value gave null
							   ) * 0.00015  -- user-defined factor for VERTICAL EXAGGERATION
						)
					)
				)
			)
		)
	)
)
